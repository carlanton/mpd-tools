plugins {
    id 'idea'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group 'io.lindstrom'
version '0.15'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.18.1'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.3'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation('org.junit.jupiter:junit-jupiter-params:5.10.3')
    testImplementation('org.xmlunit:xmlunit-matchers:2.10.0')
    testImplementation('org.apache.commons:commons-lang3:3.17.0')
}

test {
    useJUnitPlatform()
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

publishing {
    repositories {
        maven {
            def releaseRepo = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotRepo = "https://oss.sonatype.org/content/repositories/snapshots/"

            name = "OSSRH"
            url = isReleaseVersion ? releaseRepo : snapshotRepo

            credentials {
                username = findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
                password = findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            artifactId = 'mpd-parser'

            from components.java

            pom {
                groupId = 'io.lindstrom'
                name = 'mpd-parser'
                description = 'DASH MPD parser'
                url = 'https://github.com/carlanton/mpd-tools'
                packaging = 'jar'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/carlanton/mpd-tools/blob/master/LICENSE'
                    }
                }

                scm {
                    url = 'https://github.com/carlanton/mpd-tools.git'
                    connection = 'scm:git:https://github.com/carlanton/mpd-tools.git'
                    developerConnection = 'scm:git:git@github.com:carlanton/mpd-tools.git'
                }

                developers {
                    developer {
                        id = 'antonlindstrom'
                        name = 'Anton Lindstr√∂m'
                        email = 'carlantonlindstrom@gmail.com'
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
